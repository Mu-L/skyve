apply plugin: 'ear'
apply plugin: 'war'

ext {
    javaeePath = "javaee"
    earPath = "${javaeePath}/skyve.ear"
    warPath = "${earPath}/skyve.war"
}

sourceSets {
    main {
        java {
            srcDirs = ["src/skyve"]
            outputDir = file("$earPath/apps.jar")
        }

        resources {
            srcDirs = ["src/skyve"]
            exclude "**/*.java"
            output.resourcesDir = "$earPath/apps.jar"
        }
    }
    web {
        java {
            srcDirs = ['src/web']
            outputDir = file("$warPath/WEB-INF/classes")
        }
    }
    test {
        java {
            srcDirs = ["src/test", "src/generatedTest"]
        }
    }
}

assemble.dependsOn compileWebJava

war {
    archiveName 'skyve.war'

    from (warPath) {
        include '**/*.html'
        include '**/*.xhtml'
        include '**/*.jsp'
        include '**/*.js'
        include '**/*.gif'
        exclude 'WEB-INF'
    }

    into ('WEB-INF') {
        from ("$warPath/WEB-INF") {
            include 'faces-config.xml'
            include 'jboss-classloading.xml'
        }
    }

    into ('WEB-INF/resources') {
        from ("$warPath/WEB-INF/resources")
    }
}

ear {
    archiveName 'skyve.ear'

    into ('META-INF') {
        from ("$earPath/META-INF") {
            exclude 'application.xml'
        }
    }

    from war.archivePath

    deploymentDescriptor {
        displayName = "skyve"
    }

    dependsOn war
}

configurations {
    warLib {
        transitive = false
    }
    compileOnly.extendsFrom warLib
}

dependencies {
    /* earlib dependencies are copied into ear/lib */
    earlib project(":skyve-core")
    earlib project(":skyve-ext")

    earlib "commons-collections:commons-collections:${commonsCollectionsVersion}"
    earlib "commons-validator:commons-validator:${commonsValidatorVersion}"
    earlib "net.coobird:thumbnailator:${thumbnailatorVersion}"
    earlib "net.sf.ehcache:ehcache-core:${ehcacheVersion}"
    earlib "eu.bitwalker:UserAgentUtils:${userAgentUtilsVersion}"
    earlib "org.elasticsearch:elasticsearch:${elasticsearchVersion}"
    earlib "org.apache.tika:tika-core:${tikaVersion}"
    earlib ("org.apache.tika:tika-parsers:${tikaVersion}") {
        exclude group: "org.ow2.asm", module: "asm-debug-all"
        exclude group: "edu.ucar", module: "netcdf"
    }
    earlib "org.hibernate:hibernate-core:${hibernateVersion}"
    earlib "org.hibernate:hibernate-entitymanager:${hibernateEntityManagerVersion}"
    earlib "org.hibernate:hibernate-spatial:${hibernateSpatialVersion}"
    earlib "cglib:cglib:${cglibVersion}"
    earlib "org.ow2.asm:asm:${asmVersion}"
    earlib ("org.ow2.asm:asm-commons:${asmVersion}") {
        exclude group: "org.ow2.asm", module: "asm-tree"
    }
    earlib "org.codehaus.groovy:groovy-all:${groovyVersion}"
    earlib ("net.sf.jasperreports:jasperreports:${jaserReportsVersion}") {
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "bouncycastle", module: "bcmail-jdk14"
        exclude group: "bouncycastle", module: "bcprov-jdk14"
        exclude group: "xml-apis", module: "xml-apis"
        exclude group: "org.bouncycastle", module: "bctsp-jdk14"
        exclude group: "eclipse", module: "jdtcore"
        exclude group: "commons-beanutils", module: "commons-beanutils"
        exclude group: "commons-collections", module: "commons-collections"
        exclude group: "org.codehaus.castor", module: "castor-xml"
    }
    earlib "org.bouncycastle:bcmail-jdk15:${bcmailJdk15Version}"
    earlib "org.apache.lucene:lucene-analyzers-common:${luceneVersion}"
    earlib "org.apache.lucene:lucene-core:${luceneVersion}"
    earlib "org.apache.lucene:lucene-expressions:${luceneVersion}"
    earlib "org.apache.lucene:lucene-grouping:${luceneVersion}"
    earlib "org.apache.lucene:lucene-highlighter:${luceneVersion}"
    earlib "org.apache.lucene:lucene-join:${luceneVersion}"
    earlib "org.apache.lucene:lucene-memory:${luceneVersion}"
    earlib "org.apache.lucene:lucene-misc:${luceneVersion}"
    earlib "org.apache.lucene:lucene-queries:${luceneVersion}"
    earlib "org.apache.lucene:lucene-queryparser:${luceneVersion}"
    earlib ("org.apache.lucene:lucene-sandbox:${luceneVersion}") {
        exclude group: "jakarta-regexp", module: "jakarta-regexp"
    }
    earlib "org.apache.lucene:lucene-spatial:${luceneVersion}"
    earlib "org.apache.lucene:lucene-suggest:${luceneVersion}"
    earlib "org.apache.poi:poi:${poiVersion}"
    earlib "com.spatial4j:spatial4j:${spatial4jVersion}"
    earlib ("xerces:xercesImpl:${xercesImplVersion}") {
        exclude group: "xml-apis", module: "xml-apis"
    }
    earlib "org.apache.xmlbeans:xmlbeans:${xmlBeansVersion}"
    earlib "org.apache.ws.xmlschema:xmlschema-core:${xmlschemaCoreVersion}"
    earlib "com.vividsolutions:jts:${jtsVersion}"
    earlib "org.quartz-scheduler:quartz:${quartzVersion}"
    earlib "org.apache.deltaspike.core:deltaspike-core-api:${deltaspikeCoreVersion}"

    earlib "org.slf4j:slf4j-log4j12:1.5.6"
    earlib "org.apache.james:apache-mime4j-core:0.7.2"
    earlib "org.apache.james:apache-mime4j-dom:0.7.2"
    earlib "org.aspectj:aspectjrt:1.6.11"
    earlib "de.l3s.boilerpipe:boilerpipe:1.1.0"
    earlib "org.dspace.xmlui.concurrent:concurrent:1.3.4"
    earlib "org.apache.pdfbox:fontbox:${pdfBoxVersion}"
    earlib "org.opengeo:geodb:${geodbVersion}"
    earlib "net.sourceforge.hatbox:hatbox:1.0.b7"
    earlib "com.googlecode.mp4parser:isoparser:1.0-RC-1"
    earlib "jdom:jdom:1.0"
    earlib ("com.uwyn:jhighlight:1.0") {
        exclude group: "javax.servlet", module: "servlet-api"
    }
    earlib "net.java.dev.jna:jna:4.1.0"
    earlib "com.googlecode.juniversalchardet:juniversalchardet:1.0.3"
    earlib "net.sourceforge.jexcelapi:jxl:2.6.10"
    earlib "net.sf.supercsv:super-csv:2.4.0"
    earlib "com.drewnoakes:metadata-extractor:2.6.2"
    earlib ("org.apache.neethi:neethi:3.0.1") {
        exclude group: "org.codehaus.woodstox", module: "woodstox-core-asl"
    }
    earlib "net.sourceforge.nekohtml:nekohtml:1.9.7"
    earlib "org.apache.pdfbox:pdfbox:${pdfBoxVersion}"
    earlib "org.apache.poi:poi-ooxml:${poiVersion}"
    earlib "org.apache.poi:poi-ooxml-schemas:${poiVersion}"
    earlib "org.apache.poi:poi-scratchpad:${poiVersion}"
    earlib "net.sf.qualitycheck:quality-check:1.3"
    earlib "rome:rome:0.9"
    earlib "org.ccil.cowan.tagsoup:tagsoup:1.2.1"
    earlib "org.gagravarr:vorbis-java-core:0.1"
    earlib "org.gagravarr:vorbis-java-tika:0.1"
    earlib "wsdl4j:wsdl4j:1.6.2"
    earlib "org.tukaani:xz:1.2"
    earlib "com.h2database:h2:${h2Version}"

    webCompile sourceSets.main.output
    webCompile project(":skyve-web")
    webCompileOnly "javax:javaee-api:${javaeeApiVersion}"

    /* runtime dependencies are copied into war/WEB-INF/lib */
    runtime (project(":skyve-web")) {
        transitive = false
    }
    runtime "org.atmosphere:atmosphere-runtime:${atmosphereRuntimeVersion}"
    runtime "commons-fileupload:commons-fileupload:${commonsFileUploadVersion}"
    runtime "com.google.code.gson:gson:${gsonVersion}"
    runtime "org.primefaces:primefaces:${primefacesVersion}"
    runtime "org.primefaces.extensions:primefaces-extensions:${primefacesExtensionsVersion}"
    runtime "org.primefaces.themes:all-themes:${primefacesAllThemesVersion}"

    /* compileOnly dependencies are used for compilation purposed only */
    compileOnly project(":skyve-tools")
    compileOnly ("javax:javaee-api:${javaeeApiVersion}") {
        exclude group: "com.sun.mail", module: "javax.mail"
        exclude group: "javax.activation", module: "activation"
    }
    // Assertions are being used in src main
    compileOnly "junit:junit:${junitVersion}"

    testCompile project(":skyve-web")
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
    testCompile "org.jboss.weld.se:weld-se:${weldSeVersion}"
    testCompile "javax:javaee-api:${javaeeApiVersion}"
}

task clearAllDependencies(type: Delete) {
    delete "$earPath/lib"
    delete "$warPath/WEB-INF/lib"
}
clean.dependsOn clearAllDependencies

task copyEarLibDependencies(type: Copy) {
    into "$earPath/lib/"
    from configurations.earlib
}
assemble.dependsOn copyEarLibDependencies

task copyWarLibDependencies(type: Copy) {
    into "$warPath/WEB-INF/lib/"
    from configurations.runtime
}
assemble.dependsOn copyWarLibDependencies

task touch {
    doLast {
        delete fileTree(javaeePath) {
            include '**/*.failed'
        }
        ant.touch(file: "${earPath}.dodeploy")
    }
}

task generateDomain(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'org.skyve.impl.generate.DomainGenerator'

    // arguments to pass to the application
    args = ["src/skyve/", // source path
            "src/test/", // test path
            "src/generatedTest/", // generated test path
            "true", // debug
            "true", // data store index foreign keys by default
            "false", // data store index names in global namespace
            "0", // data store identifier character limit
            "test,whosin,whosinintegrate"] // excluded modules
}

task generateEditView(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'org.skyve.impl.generate.ViewGenerator'

    // arguments to pass to the application
    args = ["src/skyve/", // source path
            "bizhub", // customerName
            "admin", // moduleName
            "UserCandidateContact", // documentName
            "false", // customer overridden view
            ""] // uxui overridden view name
}

task generateDefaultQueries(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'org.skyve.impl.generate.QueryGenerator'

    // arguments to pass to the application
    args = ["bizhub", // customer name
            "admin"] // module name
}

task javadocWebsite(type: JavaExec) {
    classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath

    main = 'org.skyve.impl.tools.javadoc.doctor.DoctorUtil'

    // arguments to pass to the application
    args = ["src/skyve/", // source path
            "bizhub", // customer name
            "admin"]
}

task deprecationReport(type: JavaExec) {
    classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath

    main = 'modules.DeprecationReport'

    // arguments to pass to the application
    args = ["src/skyve/", // source path
            "bizhub", // customer name
            "admin"]
}

task buildXmlSchemas(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'org.skyve.impl.util.XMLMetaData'

    doLast {
        fileTree('.') {
            include '*.xsd'
        }.each { File file ->
            ant.move(file: file, todir: sourceSets.main.java.srcDirs[0])
        }
    }
}